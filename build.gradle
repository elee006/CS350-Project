plugins {
    id 'java'
    id 'application'
	id "org.xbib.gradle.plugin.git" version "2.0.0"
    id "org.xbib.gradle.plugin.jflex" version "1.5.0"
	id "com.github.spotbugs" version "4.6.0"
	id 'jacoco'
	id 'edu.odu.cs.report_accumulator' version '1.4'
}

java {
    sourceCompatibility = JavaVersion.toVersion(11)
    targetCompatibility = JavaVersion.toVersion(11)
}

repositories {
    jcenter()
}
sourceSets {
  main {
     jflex {
       srcDir "src/main/jflex"
     }
     java {
       srcDir "$buildDir/my-generated-sources/jflex"
     }
  }
}


dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
}

test {
    ignoreFailures = true
    useJUnitPlatform()
}

mainClassName = 'edu.odu.cs.cs350.PropertiesFile'

//Following taken from https://www.cs.odu.edu/~zeil/cs350/latest/Public/websiteDeployment/index.html

javadoc {              
   failOnError = false;
}

task reports (dependsOn: ['javadoc', 'test', 'spotbugsMain', 'jacocoTestReport']) {
	description 'Generate reports for the project website'
}

task clonePages() {                                             
    doLast {
        mkdir 'build/gh-pages'
        def thisRepo = rootProject.projectDir.toString()
        def pagesDir = "$buildDir/gh-pages"
        project.delete {
            delete pagesDir
        }
        def grgit = git.clone {
            dir = pagesDir
            uri = 'file:' + thisRepo
            bare = false
            refToCheckout = 'gh-pages'
        }
        grgit.checkout {
			branch = 'gh-pages'
		}
    }
}

task copyDocs (type: Copy, dependsOn: ['reports', 'clonePages']) {
    from "$buildDir/docs"
    into "$buildDir/gh-pages/docs"
}
task copyReports (type: Copy, dependsOn: ['reports', 'clonePages']) {
    from "$buildDir/reports"
    into "$buildDir/gh-pages/reports"
}

task deployReports (dependsOn: ['copyReports', 'copyDocs', 'clonePages']) {
	group = "Reporting"
    description  'Copies reports to the gh-pages branch in preparation for a future push to GitHub'
    doLast {
        def pagesDir = "$buildDir/gh-pages"
        def grgit = git.open {
            dir = pagesDir + "/.git"
        }
        grgit.add (update: false, patterns: ['reports/'])
        grgit.add (update: true, patterns: ['reports/'])
		grgit.add (update: false, patterns: ['docs/'])
        grgit.add (update: true, patterns: ['docs/'])
        grgit.commit {
            message = "Updating web pages"
        }
        grgit.push{}
    }
}

spotbugsMain {
    ignoreFailures = true
    effort = 'max'
    reportLevel = 'medium'
    reports {
       xml.enabled = false
       html.enabled = true
    }
	reportsDir = file("$buildDir/reports/spotbugs")
}

spotbugsTest.enabled = false

reportStats {
    reportsURL = 'https://elee006.github.io/CS350-Project/reports/'
}
